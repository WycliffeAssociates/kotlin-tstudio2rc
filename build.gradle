plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.9.22'
    id 'maven-publish'
}

group = 'org.wycliffeassociates'
version = '1.0.2'

repositories {
    mavenCentral()
    maven { url "https://nexus-registry.walink.org/repository/maven-public/" }
}

dependencies {
    def jacksonVer = '2.15.1'
    def kotlinRcVer = '0.12.0'

    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVer"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVer"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:$jacksonVer"
    implementation "org.wycliffeassociates:kotlin-resource-container:$kotlinRcVer"

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

test {
    useJUnitPlatform()
}
kotlin {
    jvmToolchain(21)
}


task sourceJar(type: Jar) {
    archiveClassifier = "sources"
    from('src/main/kotlin') {
        include '**'
    }
}
afterEvaluate {
    publishing {
        publications {
            mavenRelease(MavenPublication) {
                from components.kotlin
                groupId = group
                artifactId = 'kotlin-tstudio2rc'
                version = version
                artifact sourceJar
                pom {
                    name = 'kotlin-tstudio2rc'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://raw.githubusercontent.com/AnonymousWalker/kotlin-tstudio2rc/main/LICENSE'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                url = version.endsWith('SNAPSHOT') ? "${System.getenv("NEXUS_URL")}/repository/maven-snapshots/"
                        : "${System.getenv("NEXUS_URL")}/repository/maven-releases/"
                credentials {
                    username = System.getenv("NEXUS_USER")
                    password  = System.getenv("NEXUS_PASSWORD")
                }
            }
        }
    }
}
